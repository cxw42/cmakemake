" Vim syntax file
" Language:           make(1) output from Makefiles generated by CMake or Ninja
" Maintainer:         Chris White <cxwembedded@gmail.com>
" Version:            0.1
" Project Repository: https://github.com/cxw42/vim-cmakemake
" License:            MIT
"
" Thanks to http://vim.wikia.com/wiki/Creating_your_own_syntax_files for
" guidance.

if version < 600
    finish
elseif exists("b:current_syntax")
    finish
endif

syn case ignore

" flags
syn keyword cmmFlag warning error

" make/ninja(1) report lines
syn match cmmDriverMessage '\v^\s*%(make|ninja)>.+$'
    \ keepend oneline

" cmake percentage lines
syn match cmmPercentageLine '\v^\[\s*\d+\%\]\s.*$'
    \ keepend oneline

" log timestamp lines
syn match cmmTimestampLine '\v^[0-9-]+ [0-9:].+$'
    \ keepend oneline

" lines for commands we are running
syn match cmmCommandLine '\v^\s*docker>.+$'
    \ keepend oneline
syn match cmmCommandLine '\v^\+ .*$'
    \ keepend oneline

" header lines
syn match cmmHeaderLine '\v^\@\@.*$'
    \ keepend oneline

" Failed commands from ninja:
" >  FAILED: cmd
" >  <the command that failed>
syn region cmmFailedCommand
  \ matchgroup=cmmFailureMessage start='\v^FAIL%(ED|URE).+$'
  \ matchgroup=cmmFailedCommand end='\v.+$'

hi def link     cmmFlag         WarningMsg
hi def link     cmmDriverMessage  PreProc
hi def link     cmmPercentageLine  Comment
hi def link     cmmTimestampLine  Function
hi def link     cmmCommandLine  Special
hi def cmmHeaderLine cterm=bold ctermfg=lightblue gui=bold guifg=cornflowerblue

hi def cmmFailureMessage ctermfg=red guifg=red
hi def cmmFailedCommand ctermfg=yellow guifg=yellow

let b:current_syntax = "cmakemake"

" vi: set ts=4 sts=4 sw=4 et ai: "
